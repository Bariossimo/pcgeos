/***********************************************************************
 *
 *	FileManager Tool Demo
 *
 *	UI objects
 *	
 *	
 ***********************************************************************/

@include <stdapp.goh>
@include <fmtool.goh>
@include <foldered.goh>

/***********************************************************************
	Data Resource
***********************************************************************/
@start FMTData, data;

@visMoniker FolderEditMoniker = 'F', "Edit Folder Icon";

@if 0    
	µµ
@chunk char TextNotGEOSFile[] = "Not a GEOS File";
@chunk char TextGEOSExecutable[] = "GEOS Executable";
@chunk char TextFolder[] = "Folder";
@chunk char TextVMFile[] = "GEOS VM-File";
@chunk char TextDataFile[] = "GEOS Data-File";
@chunk char TextLink[] = "Link";
@chunk char TextUnknown[] = "Unknown";

@chunk char NoChangeNotGEOSFile[] = "You cannot change the token of this native file";
@chunk char NoChangeGEOSExecutable[] = "You cannot change the token of a GEOS executable file.";
@chunk char NoChangeFolder[] = "You cannot change the token of this folder.";
@chunk char NoChangeVMFile[] = "You cannot change the token of a GEOS VM file.";
@chunk char NoChangeDataFile[] = "You cannot change the token of a GEOS data file.";
@chunk char NoChangeLink[] = "You cannot change the token of this link.";
@chunk char NoChangeUnknown[] = "You cannot change the token of this unknown object.";
@endif

@chunk char ErrorFileNotFound[] = "Error: File or folder does not exist.";
@chunk char ErrorChangeToken[] = "Error: The token could not be changed.";
@chunk char ChangeTokenOK[] = "The token has been changed successfully.\rYou may need to update the file manager window.";
    
@chunk char ErrorNoFolders[] = "You need to select at least one folder!";    
    
@end FMTData;


/***********************************************************************
	Dialog UI Resource
***********************************************************************/
@start DialogUI, notDetachable;

    @object FolderEdInteractionClass SelectDialog = {
        GI_visMoniker = 'F', "Edit Folder Icon";
        GI_comp =   @FEDTopGroup, @FEDMainSpacer,
        	@FEDTokenListOptionsGroup, @FEDReplyBar;
        GI_states = (@default & (~GS_USABLE));
        GII_visibility = GIV_DIALOG;
        GII_attrs = @default | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
        HINT_PLACE_MONIKER_ABOVE;
        HINT_ORIENT_CHILDREN_VERTICALLY;
    }

    @object GenGlyphClass FEDMainSpacer = {
    	GI_visMoniker = "  ";
    }

/*
 * -------- TopGroup ---------------
 */

    @object GenInteractionClass FEDTopGroup = {
    	GI_comp = @SelectFileList, @FEDTokenGroup;
    	HINT_ORIENT_CHILDREN_HORIZONTALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    	HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
    	HINT_BOTTOM_JUSTIFY_CHILDREN;
    	
    }

/*
 * -------- FEDTopLeftGroup ---------------
 */
    @object GenDynamicListClass SelectFileList = {
   	GI_visMoniker = "Select a folder";
	HINT_PLACE_MONIKER_ABOVE;
	HINT_ITEM_GROUP_SCROLLABLE;
	HINT_FIXED_SIZE = { SST_AVG_CHAR_WIDTHS | 33,
			SST_LINES_OF_TEXT | 5, 5};
   	GIGI_applyMsg = MSG_FED_APPLY_FILE;
   	GDLI_queryMsg = MSG_FED_QUERY_FILE;
   	GDLI_numItems = 0;
    }
    

/*
 * -------- FEDTokenGroup ---------------
 */
    @object GenInteractionClass FEDTokenGroup = {
    	GI_comp = @TokenTokenGroup, @TokenNewGroup;
    	HINT_ORIENT_CHILDREN_HORIZONTALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    	HINT_CENTER_CHILDREN_HORIZONTALLY;
    }
    
@object GenInteractionClass TokenTokenGroup = {
	GI_visMoniker = " Icon: ";
	GI_comp = @TokenTokenDisplay,@TokenTokenID;
	HINT_ORIENT_CHILDREN_VERTICALLY;
	HINT_CENTER_CHILDREN_HORIZONTALLY;
	HINT_BOTTOM_JUSTIFY_CHILDREN;
	HINT_DRAW_IN_BOX;
	}
@object GenInteractionClass TokenTokenDisplay = {
	GI_visMoniker = " ";
	HINT_MINIMUM_SIZE = { SST_PIXELS | 70, SST_PIXELS | 35, 1 };
	HINT_CENTER_MONIKER;
	}
@object GenInteractionClass TokenTokenID = {
	GI_visMoniker = "no ID";
	HINT_CENTER_MONIKER;
	}

@object GenInteractionClass TokenNewGroup = {
	GI_visMoniker = " New Icon: ";
	GI_comp = @TokenNewDisplay,@TokenNewID;
	HINT_ORIENT_CHILDREN_VERTICALLY;
	HINT_CENTER_CHILDREN_HORIZONTALLY;
	HINT_BOTTOM_JUSTIFY_CHILDREN;
	HINT_DRAW_IN_BOX;
	}
@object GenInteractionClass TokenNewDisplay = {
	GI_visMoniker = " ";
	HINT_FIXED_SIZE = { SST_PIXELS | 70, SST_PIXELS | 35, 1 };
	HINT_CENTER_MONIKER;
	}
@object GenInteractionClass TokenNewID = {
	GI_visMoniker = "no ID";
	HINT_CENTER_MONIKER;
	}

/*
 * -------- FEDTokenList     ---------------
 */
    /*
     * Standard moniker size: 48x30
     * SuperVGA moniker size: 64x40
     */ 
    @object GenDynamicListClass FEDTokenList = {
	GIGI_selection = -1;
	GIGI_applyMsg = MSG_FED_APPLY_TOKEN_LIST;
	GDLI_queryMsg = MSG_FED_QUERY_TOKEN_LIST;
	GDLI_numItems = 0;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	HINT_ITEM_GROUP_SCROLLABLE;
	/* initially leave room for 8 tokens in SupeVGA size */
	HINT_INITIAL_SIZE = {SST_PIXELS | 8*(64+4),SST_PIXELS | (40+4), 8};
	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    }
 
/*
 * -------- FEDTokenListOptionsGroup     ---------------
 */
 
    @object GenInteractionClass FEDTokenListOptionsGroup = {
   	GI_visMoniker = "Select a new token";
	HINT_CENTER_MONIKER;
    	GI_comp = @FEDTokenInfoGroup, @FEDTokenList, @FEDTokenRangeGroup, @FEDTokenStyleGroup;
    	HINT_ORIENT_CHILDREN_VERTICALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    	HINT_DRAW_IN_BOX;
    }
    	
@object GenInteractionClass FEDTokenInfoGroup = {
	GI_comp = @FEDIconInfoCount,@FEDIconInfoDisplayed;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	HINT_TOP_JUSTIFY_CHILDREN;
	}

@object GenValueClass FEDIconInfoCount = {
	GI_visMoniker = "Tokens found:";
	GI_attrs = @default | GA_READ_ONLY;
	}
@object GenValueClass  FEDIconInfoDisplayed = {
	GI_visMoniker =" Currently displayed:";
	GI_attrs = @default | GA_READ_ONLY;
	}

@object GenInteractionClass FEDTokenRangeGroup = {
	GI_comp = @FEDManufIDMinimumValue,@FEDManufIDMaximumValue,@FEDManufIDApplayTrigger;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	/*HINT_CENTER_CHILDREN_VERTICALLY; */
	HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
	/*HINT_INCLUDE_ENDS_IN_CHILD_SPACING;	*/
	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
	}
@object WordValueClass FEDManufIDMinimumValue = {
	GI_visMoniker = "Token ID number from:";
	HINT_VALUE_NAVIGATE_TO_NEXT_FIELD_ON_RETURN_PRESS;
	}
@object WordValueClass FEDManufIDMaximumValue = {
	GI_visMoniker = "to:";
	GVLI_value = 0xFFFF;
	HINT_VALUE_NAVIGATE_TO_NEXT_FIELD_ON_RETURN_PRESS;
	}
@object GenTriggerClass FEDManufIDApplayTrigger = {
	GI_visMoniker = 'R',"Apply Range";
	GTI_actionMsg = MSG_FED_APPLY_TOKEN_RANGE;
	}
	
@object GenInteractionClass FEDTokenStyleGroup = {
	GI_comp = @FEDDisplaySizeSelector, @FEDTokenStyleSelector;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
	/*HINT_INCLUDE_ENDS_IN_CHILD_SPACING;	*/
	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
	/*HINT_TOOLBOX;*/
	}
	
@object GenItemGroupClass FEDDisplaySizeSelector = {
	GI_visMoniker = "Display size:";
	HINT_PLACE_MONIKER_TO_LEFT;
	GI_comp = @DSTiny, @DSStandard, @DSLarge, @DSHuge;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	GIGI_selection = DEFAULT_TOKEN_DISPLAY_SIZE;
	GIGI_applyMsg = MSG_FED_APPLY_TOKEN_TYPE;
	}
@object GenItemClass DSHuge = {
	GI_visMoniker = "Huge";
	GII_identifier = DS_HUGE;
	}
@object GenItemClass DSLarge = {
	GI_visMoniker = "Large";
	GII_identifier = DS_LARGE;
	}
@object GenItemClass DSStandard = {
	GI_visMoniker = " Standard ";
	GII_identifier = DS_STANDARD;
	}
@object GenItemClass DSTiny = {
	GI_visMoniker = "Tiny";
	GII_identifier = DS_TINY;
	}
	
@object GenItemGroupClass FEDTokenStyleSelector  = {
	GI_visMoniker = "Style:";
	HINT_PLACE_MONIKER_TO_LEFT;
	GI_comp = @TSIcon, @TSTool;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	GIGI_selection = DEFAULT_TOKEN_STYLE;
	GIGI_applyMsg = MSG_FED_APPLY_TOKEN_TYPE;
	}
@object GenItemClass TSIcon = {
	GI_visMoniker = " Icon ";
	GII_identifier = VMS_ICON;
	}
@object GenItemClass TSTool = {
	GI_visMoniker = " Tool ";
	GII_identifier = VMS_TOOL;
	}
	
	
	
/*
 * -------- Reply bar ---------------
 */
    @object GenInteractionClass FEDReplyBar ={
        GI_comp =   @SelectOkTriggerHolder,
                    @SelectCancelTrigger;
        HINT_MAKE_REPLY_BAR;
    }

    @object GenInteractionClass SelectOkTriggerHolder = {
    	GI_comp = @SelectOkTrigger;
    }
    @object GenTriggerClass SelectOkTrigger = {
    	GI_visMoniker = "Change Token";
    	GTI_actionMsg = MSG_FED_CHANGE_TOKEN;
    }

    @object GenTriggerClass SelectCancelTrigger = {
    	GI_visMoniker = "Cancel";
    	GTI_actionMsg = MSG_FED_QUIT_DIALOG;
        ATTR_GEN_TRIGGER_ACTION_DATA = { IC_DISMISS };
    }

@end DialogUI;


