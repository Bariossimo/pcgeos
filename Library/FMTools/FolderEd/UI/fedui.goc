/***********************************************************************
 *
 *	FileManager Tool Demo
 *
 *	UI objects
 *	
 *	
 ***********************************************************************/

@include <stdapp.goh>
@include <fmtool.goh>
@include <foldered.goh>

/***********************************************************************
	Data Resource
***********************************************************************/
@start FMTData, data;

@visMoniker FolderEditMoniker = "Edit Icon";
    
@chunk char TextNotGEOSFile[] = "Not a GEOS File";
@chunk char TextGEOSExecutable[] = "GEOS Executable";
@chunk char TextFolder[] = "Folder";
@chunk char TextVMFile[] = "GEOS VM-File";
@chunk char TextDataFile[] = "GEOS Data-File";
@chunk char TextLink[] = "Link";
@chunk char TextUnknown[] = "Unknown";
    
@end FMTData;


/***********************************************************************
	Dialog UI Resource
***********************************************************************/
@start DialogUI, notDetachable;

    @object FolderEdInteractionClass SelectDialog = {
        GI_visMoniker = "Edit Icon";
        GI_comp =   @FEDTopGroup, 
        	@FEDTokenListOptionsGroup, @FEDReplyBar;
        GI_states = (@default & (~GS_USABLE));
        GII_visibility = GIV_DIALOG;
        GII_attrs = @default | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
        HINT_PLACE_MONIKER_ABOVE;
        HINT_ORIENT_CHILDREN_VERTICALLY;
    }

/*
 * -------- TopGroup ---------------
 */

    @object GenInteractionClass FEDTopGroup = {
    	GI_comp = @FEDTopLeftGroup, @FEDTopSpacer, @FEDTopRightGroup;
    	HINT_ORIENT_CHILDREN_HORIZONTALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    	
    }

    @object GenInteractionClass FEDTopLeftGroup = {
    	GI_comp = @SelectFileList, @FileTypeText;
    	HINT_ORIENT_CHILDREN_VERTICALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    }

    @object GenGlyphClass FEDTopSpacer = {
    	GI_visMoniker = "   ";
    }

    @object GenInteractionClass FEDTopRightGroup = {
    	GI_comp = @FEDTokenGroup, @FEDTokenOptionsGroup;
    	HINT_ORIENT_CHILDREN_VERTICALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    }


/*
 * -------- FEDTopLeftGroup ---------------
 */
    @object GenDynamicListClass SelectFileList = {
   	GI_visMoniker = "Select an object";
	HINT_PLACE_MONIKER_ABOVE;
	HINT_ITEM_GROUP_SCROLLABLE;
	HINT_FIXED_SIZE = { SST_AVG_CHAR_WIDTHS | 33,
			SST_LINES_OF_TEXT | 5, 5};
   	GIGI_applyMsg = MSG_FED_APPLY_FILE;
   	GDLI_queryMsg = MSG_FED_QUERY_FILE;
   	GDLI_numItems = 0;
    }
    
    @object GenTextClass FileTypeText = {
    	GI_visMoniker = "File type:";
    	HINT_PLACE_MONIKER_TO_LEFT;
    	GI_attrs = @default | GA_READ_ONLY;
    	ATTR_GEN_TEXT_NEVER_MAKE_SCROLLABLE;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    }

/*
 * -------- FEDTokenGroup ---------------
 */
    @object GenInteractionClass FEDTokenGroup = {
    	GI_comp = @TokenTokenGroup, @TokenCreatorGroup, @TokenNewGroup;
    	HINT_ORIENT_CHILDREN_HORIZONTALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    	HINT_TOOLBOX;
    }
@object GenInteractionClass TokenTokenGroup = {
	GI_visMoniker = " Icon: ";
	GI_comp = @TokenTokenDisplay,@TokenTokenID;
	HINT_ORIENT_CHILDREN_VERTICALLY;
	HINT_CENTER_CHILDREN_HORIZONTALLY;
	HINT_BOTTOM_JUSTIFY_CHILDREN;
	HINT_DRAW_IN_BOX;
	}
@object GenInteractionClass TokenTokenDisplay = {
	GI_visMoniker = " ";
	HINT_MINIMUM_SIZE = { SST_PIXELS | 70, SST_PIXELS | 35, 1 };
	HINT_CENTER_MONIKER;
	}
@object GenInteractionClass TokenTokenID = {
	GI_visMoniker = "no ID";
	HINT_CENTER_MONIKER;
	}

@object GenInteractionClass TokenCreatorGroup = {
	GI_visMoniker = " Creator: ";
	GI_comp = @TokenCreatorDisplay,@TokenCreatorID;
	GI_states = @default & ~GS_ENABLED;
	HINT_ORIENT_CHILDREN_VERTICALLY;
	HINT_CENTER_CHILDREN_HORIZONTALLY;
	HINT_BOTTOM_JUSTIFY_CHILDREN;
	HINT_DRAW_IN_BOX;
	
	}
@object GenInteractionClass TokenCreatorDisplay = {
	GI_visMoniker = " ";
	HINT_FIXED_SIZE = { SST_PIXELS | 70, SST_PIXELS | 35, 1 };
	HINT_CENTER_MONIKER;
	HINT_TOOLBOX;
	}
@object GenInteractionClass TokenCreatorID = {
	GI_visMoniker = "no ID";
	HINT_CENTER_MONIKER;
	}

@object GenInteractionClass TokenNewGroup = {
	GI_visMoniker = " New: ";
	GI_comp = @TokenNewDisplay,@TokenNewID;
	HINT_ORIENT_CHILDREN_VERTICALLY;
	HINT_CENTER_CHILDREN_HORIZONTALLY;
	HINT_BOTTOM_JUSTIFY_CHILDREN;
	HINT_DRAW_IN_BOX;
	}
@object GenInteractionClass TokenNewDisplay = {
	GI_visMoniker = " ";
	HINT_FIXED_SIZE = { SST_PIXELS | 70, SST_PIXELS | 35, 1 };
	HINT_CENTER_MONIKER;
	}
@object GenInteractionClass TokenNewID = {
	GI_visMoniker = "no ID";
	HINT_CENTER_MONIKER;
	}


/*
 * -------- FEDTokenOptionsGroup ---------------
 */

    @object GenInteractionClass FEDTokenOptionsGroup = {
    	GI_comp = @IconOrCreatorSelector;
    	HINT_ORIENT_CHILDREN_HORIZONTALLY;
    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    }
    
    @object GenItemGroupClass IconOrCreatorSelector = {
    	GI_visMoniker = "Select token to change:";
    	GI_comp = @ChangeIconItem, @ChangeCreatorItem;
    	HINT_ORIENT_CHILDREN_HORIZONTALLY;
    	HINT_PLACE_MONIKER_ABOVE;
    	GIGI_selection = FED_CHANGE_ICON;
    	GIGI_applyMsg = MSG_FED_APPLY_CHANGE_DESTINATION;    	
    }
    @object GenItemClass ChangeIconItem = {
    	GI_visMoniker = "Change Icon";
    	GII_identifier = FED_CHANGE_ICON;
    }
    @object GenItemClass ChangeCreatorItem = {
    	GI_visMoniker = "Change Creator";
    	GII_identifier = FED_CHANGE_CREATROR;
    }
/*
 * -------- FEDTokenList     ---------------
 */
    /*
     * Standard moniker size: 48x30
     * SuperVGA moniker size: 64x40
     */ 
    @object GenDynamicListClass FEDTokenList = {
   	GI_visMoniker = "Select a new token";
	HINT_PLACE_MONIKER_ABOVE;
	GIGI_selection = -1;
	GIGI_applyMsg = MSG_FED_APPLY_TOKEN_LIST;
	GDLI_queryMsg = MSG_FED_QUERY_TOKEN_LIST;
	GDLI_numItems = 0;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	HINT_ITEM_GROUP_SCROLLABLE;
	/* initially leave room for 8 tokens in SupeVGA size */
	HINT_INITIAL_SIZE = {SST_PIXELS | 8*(64+4),SST_PIXELS | (40+4), 8};
	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    }
 
/*
 * -------- FEDTokenListOptionsGroup     ---------------
 */
 
    @object GenInteractionClass FEDTokenListOptionsGroup = {
    	GI_comp = @FEDTokenList, @FEDTokenInfoGroup, @FEDTokenRangeGroup;
    	HINT_ORIENT_CHILDREN_VERTICALLY;
/*    	HINT_CENTER_CHILDREN_HORIZONTALLY;
*/    	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    	HINT_DRAW_IN_BOX;
    }
 
@object GenInteractionClass FEDTokenInfoGroup = {
	GI_comp = @FEDIconInfoCount,@FEDIconInfoIDLabel,@FEDIconInfoIDText;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	HINT_TOP_JUSTIFY_CHILDREN;
	}

@object GenValueClass FEDIconInfoCount = {
	GI_visMoniker = "Tokens found: ";
	GI_attrs = @default | GA_READ_ONLY;
	}
@object GenGlyphClass FEDIconInfoIDLabel = {
	GI_visMoniker =" Current Token ID:";
	}
@object GenGlyphClass FEDIconInfoIDText = {
	GI_visMoniker ="-";
	}

@object GenInteractionClass FEDTokenRangeGroup = {
	GI_comp = @FEDManufIDMinimumValue,@FEDManufIDMaximumValue,@FEDManufIDApplayTrigger;
	HINT_ORIENT_CHILDREN_HORIZONTALLY;
	/*HINT_CENTER_CHILDREN_VERTICALLY; */
	HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
	HINT_INCLUDE_ENDS_IN_CHILD_SPACING;	
	HINT_EXPAND_WIDTH_TO_FIT_PARENT;
	}
@object WordValueClass FEDManufIDMinimumValue = {
	GI_visMoniker = "Token ID number from:";
	HINT_VALUE_NAVIGATE_TO_NEXT_FIELD_ON_RETURN_PRESS;
	}
@object WordValueClass FEDManufIDMaximumValue = {
	GI_visMoniker = "to:";
	GVLI_value = 0xFFFF;
	HINT_VALUE_NAVIGATE_TO_NEXT_FIELD_ON_RETURN_PRESS;
	}
@object GenTriggerClass FEDManufIDApplayTrigger = {
	GI_visMoniker = 'R',"Apply Range";
	GTI_actionMsg = MSG_FED_APPLY_TOKEN_RANGE;
	}

/*
 * -------- Reply bar ---------------
 */
    @object GenInteractionClass FEDReplyBar ={
        GI_comp =   @SelectOkTrigger,
                    @SelectCancelTrigger;
        HINT_MAKE_REPLY_BAR;
    }

    @object GenTriggerClass SelectOkTrigger = {
    	GI_visMoniker = "Change Icon";
    	GTI_actionMsg = MSG_FED_QUIT_DIALOG;
        ATTR_GEN_TRIGGER_ACTION_DATA = { IC_OK };
        HINT_SEEK_REPLY_BAR;
        HINT_DEFAULT_DEFAULT_ACTION;
    }

    @object GenTriggerClass SelectCancelTrigger = {
    	GI_visMoniker = "Cancel";
    	GTI_actionMsg = MSG_FED_QUIT_DIALOG;
        ATTR_GEN_TRIGGER_ACTION_DATA = { IC_DISMISS };
        HINT_SEEK_REPLY_BAR;
    }

@end DialogUI;


